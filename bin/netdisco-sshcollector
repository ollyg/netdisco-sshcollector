#!/usr/bin/perl

=head1 NAME

netdisco-sshcollector - Collect ARP data for Netdisco from devices 
without full SNMP support

=head1 SYNOPSIS

    netdisco-sshcollector [-i inputfile] [-c netdisco_config] [-w workers]

=head1 DESCRIPTION

This program collects ARP data for Netdisco from devices without 
full SNMP support. Currently, ARP tables can be retrieved from the 
following device classes: 

=over 4 

=item * Cisco ACE using Netdisco::SSHCollector::Devices::ACE

=item * F5 BigIP using Netdisco::SSHCollector::Devices::BigIP

=back

The collected arp entries are then directly stored in the netdisco 
database.

=head1 USAGE

=over 4 

=item C<-i inputfile> 
 
Data is collected from the machines specified in this file. The 
format is five tab-separated columns: hostname, ipaddress, 
username, password, devicetpye. Blank lines and lines with a 
leading # are ignored.

Devicetype is the final part of the classname to be instantiated
to query the host. 
If the hostname is "-", the SSH session will be established using the
ip address.
If the password is "-", public key authentication will be attempted.

Parameter defaults to ../etc/netdisco-sshcollector-hosts.csv. 

=item C<-c netdisco_config>

Used to find the database of the local netdisco installation.

Parameter defaults to /usr/share/netdisco/netdisco.conf. 


=item C<-w workers>

Number of parallel processes launched by Net::OpenSSH::Parallel.

Parameter defaults to 10.

=back

=head1 INPUT FILE EXAMPLE

 #hostname	ipaddress	user	password	devicetype
 host1	10.0.0.1	bob	$ecret77	BigIP
 -	10.0.1.5	alice	-	ACE

=head1 ADDING DEVICES

Additional device classes can be easily integrated just by adding
and additonal Netdisco::SSHCollector::Devices class. This class 
must implement an C<arpnip($hostname, $ssh)> method which returns
an array of hashrefs in the format

   { ip => IPADDR, mac => MACADDR }

=head1 DEPENDENCIES

=over 4 

=item C<Cache::Cache>

=item C<Net::OpenSSH>

=item C<Net::OpenSSH::Parallel>

=item C<netdisco.pm>

=back

The first three modules can be obtained from CPAN. Alternatively if you
can't use CPAN and your distro doesn't provide these packages, 

The module netdisco.pm comes with the netdisco installation.

It is likely necessary to add the netdisco base directory 
(containing netdisco and netdisco.pm) to PERLLIB or the
B<use lib> statement.


=cut

use lib qw(../lib ./lib /appl/netdisco /opt/netdisco);
use warnings;
use strict;

use Cache::FileCache;
use Net::OpenSSH::Parallel;
use Data::Dumper;
use Getopt::Long;
use netdisco qw(config add_arp sql_begin sql_commit);

use Netdisco::SSHCollector::Devices::BigIP; 

my $namespace = "netdisco-sshcollector";
my $oconfig = "/usr/share/netdisco/netdisco.conf";
my $oinput = "../etc/netdisco-sshcollector-hosts.csv";
my $oworkers = 10;
my $cache = Cache::FileCache->new({namespace => $namespace});

exit main();

sub main {

    GetOptions(
        "config=s" => \$oconfig,
        "input=s" => \$oinput,
        "workers=i" => \$oworkers,
    );

    die "Can't read netdisco config: $oconfig" unless (-r $oconfig);
    die "Can't read input file: $oinput" unless (-r $oinput);

    my $pssh = Net::OpenSSH::Parallel->new(
        workers => $oworkers, connections => $oworkers
    );

    $cache->clear;
    my @input = readinput();

    foreach my $host (@input){
        print Dumper ($host) ;
        $pssh->add_host($host->{hostname}, 
            user => $host->{user}, 
            password => $host->{password} eq "-" ? undef : $host->{password}, 
            vars => {devicetype => $host->{devicetype}},
            master_opts => [
                -o => "StrictHostKeyChecking=no",
                -o => "BatchMode=yes"]
        );
    }

    $pssh->push('*', parsub => \&process);
    $pssh->run;
    
    for my $host (@input){
        my $arpentries = $cache->get($host->{hostname});
        print $host->{hostname} . ": " .@${arpentries}. " entries obtained\n";
        store_arpentries($arpentries);
    }
}

sub process {

    my ($hostlabel, $ssh, @args) = @_; 

    my $class = "Netdisco::SSHCollector::Devices::".$ssh->get_var("devicetype");
    my $device = $class->new();
    my @arpentries = $device->arpnip($hostlabel, $ssh, @args);
    $cache->set("$hostlabel", \@arpentries)

}

sub store_arpentries {

    my ($arpentries) = @_;

    config($oconfig);
    sql_begin(['node_ip']);

    foreach my $arpentry(@{$arpentries}){

        # skip broadcast/vrrp/hsrp and other wierdos
        next if $arpentry->{mac} =~ /^(ff:ff:ff:ff:ff:ff|0:0:5e:0|0:0:c:7:ac)/i;

        print "Entry: " . $arpentry->{mac} . " " .  $arpentry->{ip} . "\n";
        add_arp($arpentry->{mac}, $arpentry->{ip});
    }

    sql_commit();
}

sub readinput {

    open(INPUT, $oinput) or die "Failed to open input file $oinput: $!";
    my @input;

    while(<INPUT>){
        next if m/^(#|$)/; 
        chomp;
        my ($hostname, $ip, $user, $password, $devicetype) = split(/\t/);
        push(@input, {
                hostname => $hostname eq "-" ? $ip : $hostname, 
                ip => $ip, 
                user => $user, password => $password, 
                devicetype => $devicetype}
        );
    }

    return @input;
}
